# - Find ApproxMVBB Installation
#
# Users can set the following variables before calling the module:
# ApproxMVBB_DIR - The preferred installation prefix for searching for ApproxMVBB. Set by the user.
#
# ApproxMVBB_CXX_FLAGS - extra flags for compilation
# ApproxMVBB_INCLUDE_DIRS - include directories
# ApproxMVBB_LIBRARY_REL  - Release library
# ApproxMVBB_LIBRARY_DGB  - Debug library
# ApproxMVBB_LIBRARIES - libraries to link with
# ApproxMVBB_COMPILE_DEFINITIONS - compile definitions which should be
# added to your project, they only define some macros for safety checks when including headers.

macro(make_library_set PREFIX)
  if (${PREFIX}_FWK)
    set(${PREFIX} ${${PREFIX}_FWK})
  elseif (${PREFIX}_REL AND ${PREFIX}_DBG)
    set(${PREFIX} optimized ${${PREFIX}_REL} debug ${${PREFIX}_DBG})
  elseif (${PREFIX}_REL)
    set(${PREFIX} ${${PREFIX}_REL})
  elseif (${PREFIX}_DBG)
    set(${PREFIX} ${${PREFIX}_DBG})
  endif()
endmacro(make_library_set)


macro(findpkg_finish PREFIX)
  # skip if already processed during this run
  if (NOT ${PREFIX}_FOUND)
    if ( (NOT "${ARGV1}") AND ${PREFIX}_INCLUDE_DIR AND ${PREFIX}_LIBRARY)
      set(${PREFIX}_FOUND TRUE )
      set(${PREFIX}_INCLUDE_DIRS ${${PREFIX}_INCLUDE_DIR})
      set(${PREFIX}_LIBRARIES ${${PREFIX}_LIBRARY}  )
      if (NOT ${PREFIX}_FIND_QUIETLY)
        message(STATUS "Found ${PREFIX}")
      endif ()
    elseif ( "${ARGV1}" AND ${PREFIX}_INCLUDE_DIR)
      set(${PREFIX}_FOUND TRUE)
      set(${PREFIX}_INCLUDE_DIRS ${${PREFIX}_INCLUDE_DIR} )
      set(${PREFIX}_LIBRARIES ${${PREFIX}_LIBRARY} )
      if (NOT ${PREFIX}_FIND_QUIETLY)
        message(STATUS "Found ${PREFIX}: No Library, only Headers")
      endif ()
    else ()
      if (NOT ${PREFIX}_FIND_QUIETLY)
        message(STATUS "Could not locate ${PREFIX}")
      endif ()
      if (${PREFIX}_FIND_REQUIRED)
        message(FATAL_ERROR "Required library ${PREFIX} not found! Install the library (including dev packages) and try again. If the library is already installed, set the missing variables manually in cmake.")
      endif ()
    endif ()

    # add dependency libraries
    if (${PREFIX}_LIBRARIES_DEP)
        list(APPEND ${PREFIX}_LIBRARIES ${${PREFIX}_LIBRARIES_DEP})
    endif()

    mark_as_advanced(${PREFIX}_INCLUDE_DIR ${PREFIX}_LIBRARY ${PREFIX}_LIBRARY_REL ${PREFIX}_LIBRARY_DBG ${PREFIX}_LIBRARY_FWK)
  endif ()
endmacro(findpkg_finish)


set( ApproxMVBB_CXX_FLAGS "-std=c++11" CACHE STRING "Build flags when linking with ApproxMVBB")


#Find Include Headers
find_path(ApproxMVBB_INCLUDE_DIR NAMES "@ApproxMVBB_INCLUDE_CHARATERISTIC_HEADER@" HINTS @ApproxMVBB_INC_INSTALL_DIR@ ${ApproxMVBB_DIR})
message(STATUS "ApproxMVBB include dir:  " ${ApproxMVBB_INCLUDE_DIR})

#Find libraries in @ApproxMVBB_LIBRARY_INSTALL_PATH@
find_library(ApproxMVBB_LIBRARY_REL NAMES "@ApproxMVBB_LIBRARY_NAME_REL@" HINTS @ApproxMVBB_LIB_INSTALL_DIR@ ${ApproxMVBB_DIR} NO_DEFAULT_PATH)
find_library(ApproxMVBB_LIBRARY_DBG NAMES "@ApproxMVBB_LIBRARY_NAME_DBG@" HINTS @ApproxMVBB_LIB_INSTALL_DIR@ ${ApproxMVBB_DIR} NO_DEFAULT_PATH)
message(STATUS "ApproxMVBB found (release lib): " ${ApproxMVBB_LIBRARY_REL})
message(STATUS "ApproxMVBB found (debug lib): " ${ApproxMVBB_LIBRARY_DBG})



# find dependecies Eigen3, meta and pugiXMl
# ========================================================================================================================================
SET(OLD_PATH ${CMAKE_MODULE_PATH})
LIST(APPEND CMAKE_MODULE_PATH ${CMAKE_CURRENT_LIST_DIR})
message(STATUS "Changed path temporarly to: " ${CMAKE_MODULE_PATH})
message(STATUS "Try finding Eigen3")
# Define Eigen =====================
find_package(Eigen3 REQUIRED)
set(EIGEN_INCLUDE_DIR ${EIGEN3_INCLUDE_DIR})
list(APPEND ApproxMVBB_INCLUDE_DIR ${EIGEN_INCLUDE_DIR})
# ==================================

# Define Meta if KdTree Support is selected
IF ( ";${ApproxMVBB_FIND_COMPONENTS};" MATCHES ";SUPPORT_KDTREE;" )
    message(STATUS "ApproxMVBB: add support for KdTree")
    message(STATUS "Try finding Meta")
    # Define Meta ======================
    find_package(Meta REQUIRED)
    # ==================================
    LIST(APPEND ApproxMVBB_INCLUDE_DIR ${Meta_INCLUDE_DIR})
    SET(ApproxMVBB_DEPENDING_TARGETS ${Meta_TARGET})
    LIST(APPEND ApproxMVBB_COMPILE_DEFINITIONS "-DApproxMVBB_SUPPORT_KDTREE")
ENDIF()

IF ( ";${ApproxMVBB_FIND_COMPONENTS};" MATCHES ";SUPPORT_XML;" )
  message(STATUS "ApproxMVBB: add support for XML")
  message(STATUS "Try finding pugixml")
  FIND_PACKAGE(PugiXML REQUIRED)
  LIST(APPEND ApproxMVBB_INCLUDE_DIR   ${PUGIXML_INCLUDE_DIR} )
  LIST(APPEND ApproxMVBB_LIBRARIES_DEP ${PUGIXML_LIBRARIES})
  LIST(APPEND ApproxMVBB_COMPILE_DEFINITIONS "-DApproxMVBB_SUPPORT_XML")
ENDIF()

IF(ApproxMVBB_COMPILE_DEFINITIONS)
  message(STATUS "ApproxMVBB: add compile definitions '\${ApproxMVBB_COMPILE_DEFINITIONS}' = '${ApproxMVBB_COMPILE_DEFINITIONS}' to your target.")
ENDIF()
# ========================================================================================================================================

SET(CMAKE_MODULE_PATH ${OLD_PATH})



make_library_set(ApproxMVBB_LIBRARY)
findpkg_finish(ApproxMVBB)

include(FindPackageHandleStandardArgs)
## handle the QUIETLY and REQUIRED arguments
## if all listed variables are TRUE
find_package_handle_standard_args(ApproxMVBB DEFAULT_MSG ApproxMVBB_LIBRARIES ApproxMVBB_INCLUDE_DIR)




MARK_AS_ADVANCED(
    ApproxMVBB_CXX_FLAGS
    ApproxMVBB_INCLUDE_DIRS
    ApproxMVBB_LIBRARY_REL
    ApproxMVBB_LIBRARY_DBG
    ApproxMVBB_LIBRARIES
    ApproxMVBB_DEPENDING_TARGETS
    ApproxMVBB_COMPILE_DEFINITIONS
)
